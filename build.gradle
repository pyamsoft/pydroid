/*
 * Copyright 2022 Peter Kenji Yamanaka
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
  ext {
    kotlin = "1.9.22"
    coroutines = "1.8.0"

    // Compose
    compose = "1.6.3"
    composeCompiler = "1.5.10"
    composeMaterial = "1.6.3"

    // AndroidX
    core = "1.12.0"
    activity = "1.8.2"
    lifecycle = "2.7.0"

    // Test Runner
    testRunner = "1.5.2"
  }
}

plugins {
  // Android AGP
  // https://developer.android.com/studio/build#top-level
  id("com.android.library") version "8.3.1" apply false

  // Fix Android build cache
  // https://github.com/gradle/android-cache-fix-gradle-plugin
  id("org.gradle.android.cache-fix") version "3.0.1" apply false

  // Kotlin
  // https://developer.android.com/studio/build#top-level
  id("org.jetbrains.kotlin.android") version "${kotlin}" apply false

  // Dokka
  // https://github.com/Kotlin/dokka
  id("org.jetbrains.dokka") version "1.9.20" apply false

  // Gradle version checker
  // https://github.com/ben-manes/gradle-versions-plugin
  id("com.github.ben-manes.versions") version "0.51.0" apply false

  // Version Filter Plugin
  // https://github.com/janderssonse/gradle-versions-filter-plugin
  id("se.ascp.gradle.gradle-versions-filter") version "0.1.16" apply false

  // Spotless
  // https://github.com/diffplug/spotless
  id("com.diffplug.spotless") version "6.25.0" apply false

  // Validate the public API
  // https://github.com/Kotlin/binary-compatibility-validator
  id("org.jetbrains.kotlinx.binary-compatibility-validator") version "0.14.0"

  // Gradle Doctor
  // https://runningcode.github.io/gradle-doctor/
  id("com.osacky.doctor") version "0.9.2" apply true
}

subprojects {
  apply plugin: "com.android.library"
  apply plugin: "org.gradle.android.cache-fix"
  apply plugin: "kotlin-android"
  apply plugin: "org.jetbrains.dokka"
  apply plugin: "maven-publish"

  // Configure matching toolchains
  // https://kotlinlang.org/docs/gradle-configure-project.html#2c42d1dc
  //
  // Or maybe don't
  // https://jakewharton.com/gradle-toolchains-are-rarely-a-good-idea/
  // kotlin {
  //   jvmToolchain(17)
  // }
  // java {
  //   toolchain {
  //     languageVersion.set(JavaLanguageVersion.of(17))
  //   }
  // }

  android {
    compileSdk = 34

    //noinspection GroovyMissingReturnStatement
    defaultConfig {
      minSdk = 22

      // For vector drawables
      vectorDrawables.useSupportLibrary = true

      // Consumer proguard file
      consumerProguardFiles("consumer-rules.pro")

      // Only English, I don"t have any translations
      resourceConfigurations = ["en"]
    }

    compileOptions {
      sourceCompatibility = JavaVersion.VERSION_17
      targetCompatibility = JavaVersion.VERSION_17

      // Flag to enable support for the new language APIs
      coreLibraryDesugaringEnabled = true
    }

    kotlinOptions {
      jvmTarget = JavaVersion.VERSION_17.getMajorVersion()
    }

    publishing {
      singleVariant("release") {
        withSourcesJar()
        withJavadocJar()
      }
    }

    buildTypes {
      debug {
        minifyEnabled = false
        shrinkResources = false
      }

      release {
        minifyEnabled = false
        shrinkResources = false
      }
    }

    buildFeatures {
      buildConfig = false
    }
  }

  project.afterEvaluate {
    publishing {
      publications {
        PYDroid(MavenPublication) {
          from project.components.release

          artifactId project.name
          groupId "com.github.pyamsoft.pydroid"
          version "26.9.4"
        }
      }
    }
  }

  dependencies {
    coreLibraryDesugaring("com.android.tools:desugar_jdk_libs:2.0.4")
  }
}

allprojects {
  apply plugin: "com.diffplug.spotless"
  apply plugin: "com.github.ben-manes.versions"
  apply plugin: "se.ascp.gradle.gradle-versions-filter"

  repositories {
    mavenLocal()
    gradlePluginPortal()
    mavenCentral()
    google()

    // Jitpack
    maven {
      setUrl("https://jitpack.io")
      content {
        includeGroup("com.github.pyamsoft")
      }
    }
  }

  // Java compile
  tasks.withType(JavaCompile).configureEach {
    options.compilerArgs += [
      "-Xlint:unchecked",
      "-Xlint:deprecation",
    ]
    options.deprecation = true
  }

  // Spotless
  spotless {
    java {
      target("src/**/*.java")

      removeUnusedImports()
      trimTrailingWhitespace()
      endWithNewline()
      indentWithSpaces(2)
    }
    kotlin {
      target("src/**/*.kt", "*.kts")
      ktfmt()

      trimTrailingWhitespace()
      endWithNewline()
      indentWithSpaces(2)
    }
    groovyGradle {
      target("*.gradle")
      greclipse()

      trimTrailingWhitespace()
      endWithNewline()
      indentWithSpaces(2)
    }
  }
}
