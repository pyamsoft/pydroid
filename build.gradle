/*
 * Copyright 2025 pyamsoft
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at:
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

import com.deezer.caupain.model.StabilityLevelPolicy
import com.deezer.caupain.plugin.DependenciesUpdateTask
import org.jetbrains.kotlin.gradle.dsl.JvmTarget
import org.jetbrains.kotlin.gradle.dsl.KotlinVersion

// https://developer.android.com/build/releases/agp-9-0-0-release-notes#runtime-dependency-on-kotlin-gradle-plugin
buildscript {
  dependencies {
    //noinspection DifferentKotlinGradleVersion
    classpath("org.jetbrains.kotlin:kotlin-gradle-plugin:${libs.versions.kotlin.get()}")
  }
}

plugins {
  // Android AGP
  // https://developer.android.com/studio/build#top-level
  alias(libs.plugins.android) apply false

  // Fix Android build cache
  // https://github.com/gradle/android-cache-fix-gradle-plugin
  alias(libs.plugins.android.cacheFix) apply false

  // Jetpack Compose Compiler
  // https://android-developers.googleblog.com/2024/04/jetpack-compose-compiler-moving-to-kotlin-repository.html
  alias(libs.plugins.compose.compiler) apply false

  // Dokka
  // https://github.com/Kotlin/dokka
  alias(libs.plugins.dokka) apply false

  // Spotless
  // https://github.com/diffplug/spotless
  alias(libs.plugins.spotless) apply false

  // Gradle Doctor
  // https://runningcode.github.io/gradle-doctor/
  alias(libs.plugins.doctor) apply true

  // Caupain
  // https://github.com/deezer/caupain/blob/main/gradle-plugin/README.md
  alias(libs.plugins.caupain) apply true
}

subprojects {
  apply(plugin: libs.plugins.android.asProvider().get().pluginId)
  apply(plugin: libs.plugins.android.cacheFix.get().pluginId)
  apply(plugin: libs.plugins.dokka.get().pluginId)
  apply(plugin: "maven-publish")

  android {
    compileSdk = libs.versions.compileSdk.get().toInteger()

    //noinspection GroovyMissingReturnStatement
    defaultConfig {
      minSdk = libs.versions.minSdk.get().toInteger()

      // For vector drawables
      vectorDrawables.useSupportLibrary = true

      // Consumer proguard file
      consumerProguardFiles("consumer-rules.pro")
    }

    compileOptions {
      sourceCompatibility = JavaVersion.VERSION_21
      targetCompatibility = JavaVersion.VERSION_21

      // Flag to enable support for the new language APIs
      coreLibraryDesugaringEnabled = true
    }

    kotlin {
      compilerOptions {
        languageVersion = KotlinVersion.KOTLIN_2_3
        jvmTarget = JvmTarget.JVM_21
        freeCompilerArgs.add("-Xexplicit-api=strict")
      }
    }

    publishing {
      singleVariant("release") {
        withSourcesJar()
        withJavadocJar()
      }
    }

    buildTypes {
      debug {
        minifyEnabled = false
        shrinkResources = false
      }

      release {
        minifyEnabled = false
        shrinkResources = false
      }
    }

    buildFeatures {
      buildConfig = false
    }
  }

  tasks.withType(JavaCompile).configureEach {
    options.compilerArgs.add("-Xlint:unchecked")
    options.compilerArgs.add("-Xlint:deprecation")
    options.deprecation = true
  }

  project.afterEvaluate {
    publishing {
      publications {
        PYDroid(MavenPublication) {
          from project.components.release

          artifactId project.name
          groupId "com.github.pyamsoft.pydroid"
          version "29.2.1"
        }
      }
    }
  }

  dependencies {
    coreLibraryDesugaring(libs.android.desugar)
  }
}

allprojects {
  // Need to do this instead of applying Spotless in the plugin block
  // or we get an error about duplicate plugins
  //
  // and we can't just apply this toplevel or it won't run on all the subprojects
  apply(plugin: libs.plugins.spotless.get().pluginId)

  repositories {
    gradlePluginPortal()
    google()
    mavenCentral()

    // Jitpack
    maven {
      setUrl("https://jitpack.io")
      content {
        includeGroup("com.github.pyamsoft")
      }
    }
  }

  // Spotless
  spotless {
    java {
      target("src/**/*.java")

      removeUnusedImports()
      trimTrailingWhitespace()
      endWithNewline()
      leadingTabsToSpaces(2)
    }
    kotlin {
      target("src/**/*.kt")
      ktfmt()

      trimTrailingWhitespace()
      endWithNewline()
      leadingTabsToSpaces(2)
    }
    kotlinGradle {
      target("*.gradle.kts")
      ktfmt()

      trimTrailingWhitespace()
      endWithNewline()
      leadingTabsToSpaces(2)
    }
    groovyGradle {
      target("*.gradle")
      greclipse()

      trimTrailingWhitespace()
      endWithNewline()
      leadingTabsToSpaces(2)
    }
  }

  // Caupain Version Strategy
  tasks.withType(DependenciesUpdateTask).configureEach {
    selectIf(StabilityLevelPolicy.INSTANCE)
  }
}
