/*
 * Copyright 2020 Peter Kenji Yamanaka
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
  ext {
    kotlin_version = '1.5.20'
    dokka_version = "1.5.0"
    agp_version = "4.2.2"
  }
  repositories {
    mavenLocal()
    google()
    mavenCentral()

    maven {
      url "https://jitpack.io/"
    }

    maven {
      url "https://plugins.gradle.org/m2/"
    }
  }

  ext {

    minSdk = 22
    maxSdk = 30

    // Kotlin
    coroutines = '1.5.1'

    // AndroidX
    annotation = "1.2.0"
    core = "1.6.0"
    constraintLayout = "2.0.4"
    lifecycle = "2.3.1"
    lifecycleExtensions = "2.2.0"
    preference = "1.1.1"
    recyclerView = "1.2.1"
    fragment = "1.3.5"
    appCompat = "1.3.0"
    swipeRefresh = "1.1.0"
    startup = "1.0.0"

    // Material Design
    materialDesign = "1.4.0"

    // Us
    cachify = "0.0.23"
    highlander = "0.0.18"

    // Network
    moshi = "1.12.0"
    retrofit = '2.9.0'
    okhttp = "4.9.1"

    // Logging
    timber = "4.7.1"

    // Image loading
    glide = '4.12.0'

    // Recyclerview
    decorator = "1.3.0"
    fastscroll = "1.1.7"

    // In app updates and reviews
    playCore = "1.10.0"
    playCoreKtx = "1.8.1"

    // In app billing
    playBilling = "4.0.0"
  }

  dependencies {
    classpath "com.android.tools.build:gradle:$agp_version"

    // Gradle version checker
    classpath 'com.github.ben-manes:gradle-versions-plugin:0.39.0'

    // Kotlin
    classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"

    // Dokka
    classpath "org.jetbrains.dokka:dokka-gradle-plugin:$dokka_version"

    // Spotless
    classpath "com.diffplug.spotless:spotless-plugin-gradle:5.14.1"
  }
}

subprojects {
  apply plugin: 'com.android.library'
  apply plugin: 'kotlin-android'
  apply plugin: 'kotlin-kapt'
  apply plugin: 'org.jetbrains.dokka'
  apply plugin: 'maven-publish'

  final String POM_GROUP = "com.github.pyamsoft.pydroid"
  final String POM_VERSION = "23.1.0"

  // Kapt flags
  kapt {
    correctErrorTypes = true
    useBuildCache = true
    mapDiagnosticLocations = true

    javacOptions {
      option("-Xlint:unchecked")
      option("-Xlint:deprecation")
      option("-Xmaxerrs", 10000)
    }
  }

  android {
    compileSdkVersion rootProject.ext.maxSdk

    //noinspection GroovyMissingReturnStatement
    defaultConfig {
      minSdkVersion rootProject.ext.minSdk

      // For vector drawables
      vectorDrawables.useSupportLibrary = true

      // Consumer proguard file
      consumerProguardFiles "consumer-rules.pro"

      // Only English, I don't have any translations
      resConfigs "en"

      compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
      }

      kotlinOptions {
        jvmTarget = "1.8"
      }

    }

    buildTypes {
      debug {
        minifyEnabled false
        shrinkResources false
      }

      release {
        minifyEnabled false
        shrinkResources false
      }
    }

    buildFeatures {
      buildConfig false
    }
  }

  project.afterEvaluate {
    task androidSourcesJar(type: Jar) {
      archiveClassifier.convention('sources')
      archiveClassifier.set('sources')
      from android.sourceSets.main.java.source
    }

    task androidJavadocsJar(type: Jar, dependsOn: dokkaJavadoc) {
      archiveClassifier.convention('javadoc')
      archiveClassifier.set('javadoc')
      from "$buildDir/dokka"
    }

    artifacts {
      archives androidSourcesJar
      archives androidJavadocsJar
    }

    publishing {
      publications {
        PYDroid(MavenPublication) {
          artifact androidSourcesJar
          artifact androidJavadocsJar
          from project.components.release

          groupId POM_GROUP
          artifactId project.name
          version POM_VERSION
        }
      }
    }
  }
}

allprojects {
  apply plugin: "com.diffplug.spotless"
  apply plugin: 'com.github.ben-manes.versions'

  spotless {
    java {
      target "src/**/*.java"

      removeUnusedImports()
      trimTrailingWhitespace()
      endWithNewline()
      indentWithSpaces(2)
    }
    kotlin {
      target "src/**/*.kt"
      ktfmt()

      trimTrailingWhitespace()
      endWithNewline()
      indentWithSpaces(2)
    }
    groovyGradle {
      target "*.gradle"
      greclipse()

      trimTrailingWhitespace()
      endWithNewline()
      indentWithSpaces(2)
    }
  }

  def isNonStable = { String version ->
    def stableKeyword = ['RELEASE', 'FINAL', 'GA'].any { version.toUpperCase().contains(it) }
    def regex = /^[0-9,.v-]+(-r)?$/
    return !stableKeyword && !(version ==~ regex)
  }

  tasks.named("dependencyUpdates").configure {
    rejectVersionIf {
      isNonStable(it.candidate.version)
    }
  }

  repositories {
    mavenLocal()
    google()
    mavenCentral()

    maven {
      url "https://jitpack.io/"
    }

    maven {
      url "https://plugins.gradle.org/m2/"
    }
  }

  tasks.withType(JavaCompile) {
    options.compilerArgs << '-Xlint:unchecked' << '-Xlint:deprecation'
    options.deprecation = true
  }
}
