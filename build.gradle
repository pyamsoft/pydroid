
/*
 * Copyright 2022 Peter Kenji Yamanaka
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
  ext {
    kotlin_version = '1.7.21'
    dokka_version = "1.7.20"
    agp_version = "7.3.1"
    compose_version = "1.3.1"
    compose_compiler_version = "1.3.2"
  }
  repositories {
    mavenLocal()
    google()
    mavenCentral()

    maven {
      url "https://plugins.gradle.org/m2/"
    }

    maven {
      url 'https://jitpack.io'
      content {
        includeGroup("com.github.pyamsoft")
      }
    }
  }

  ext {

    minSdk = 22
    maxSdk = 33

    // Compose
    composeActivity = "1.6.1"
    accompanist = "0.27.1"
    coil = "2.2.2"

    // Kotlin
    coroutines = '1.6.4'

    // AndroidX
    annotation = "1.5.0"
    core = "1.9.0"
    lifecycle = "2.5.1"
    fragment = "1.5.4"
    startup = "1.1.1"
    appCompat = "1.5.1"

    // Material Design
    materialDesign = "1.7.0"

    // Us
    cachify = "0.1.0"
    highlander = "0.1.0"

    // Google Play libraries
    appUpdate = "2.0.0"
    review = "2.0.0"
    billing = "5.1.0"

    // Needed just for androidx.preference.PreferenceManager
    // Eventually, big G may push for DataStore being a requirement, which will be pain
    // This pulls in all the UI bits too, which is a little lame.
    preference = "1.2.0"
  }

  dependencies {
    classpath "com.android.tools.build:gradle:$agp_version"

    // Gradle version checker
    classpath 'com.github.ben-manes:gradle-versions-plugin:0.44.0'

    // Kotlin
    classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"

    // Dokka
    classpath "org.jetbrains.dokka:dokka-gradle-plugin:$dokka_version"

    // Spotless
    classpath "com.diffplug.spotless:spotless-plugin-gradle:6.11.0"
  }
}

plugins {
  // Fix Android build cache
  // https://github.com/gradle/android-cache-fix-gradle-plugin
  id "org.gradle.android.cache-fix" version "2.6.0" apply false

  // Validate the public API
  id 'org.jetbrains.kotlinx.binary-compatibility-validator' version '0.12.1'
}

subprojects {
  apply plugin: 'com.android.library'
  apply plugin: 'kotlin-android'
  apply plugin: 'kotlin-kapt'
  apply plugin: 'org.jetbrains.dokka'
  apply plugin: 'maven-publish'
  apply plugin: "org.gradle.android.cache-fix"

  final POM_GROUP = "com.github.pyamsoft.pydroid"
  final POM_VERSION = "25.0.0"


  // Kapt flags
  kapt {
    correctErrorTypes = true
    useBuildCache = true
    mapDiagnosticLocations = true

    javacOptions {
      option("-Xlint:unchecked")
      option("-Xlint:deprecation")
      option("-Xmaxerrs", 10000)
    }
  }

  android {
    compileSdkVersion rootProject.ext.maxSdk

    //noinspection GroovyMissingReturnStatement
    defaultConfig {
      minSdkVersion rootProject.ext.minSdk

      // For vector drawables
      vectorDrawables.useSupportLibrary = true

      // Consumer proguard file
      consumerProguardFiles "consumer-rules.pro"

      // Only English, I don't have any translations
      resConfigs "en"

      compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
      }

      kotlinOptions {
        jvmTarget = "11"
      }

      publishing {
        singleVariant("release") {
          // Do not include withSources and withJavadoc here because we will
          // add them with our own custom hooks below
        }
      }
    }

    buildTypes {
      debug {
        minifyEnabled false
        shrinkResources false
      }

      release {
        minifyEnabled false
        shrinkResources false
      }
    }

    buildFeatures {
      buildConfig false
    }
  }

  project.afterEvaluate {
    task androidSourcesJar(type: Jar) {
      archiveClassifier.convention('sources')
      archiveClassifier.set('sources')
      from android.sourceSets.main.java.source
    }

    task androidJavadocsJar(type: Jar, dependsOn: dokkaJavadoc) {
      archiveClassifier.convention('javadoc')
      archiveClassifier.set('javadoc')
      from "$buildDir/dokka"
    }

    artifacts {
      archives androidSourcesJar
      archives androidJavadocsJar
    }

    publishing {
      publications {
        PYDroid(MavenPublication) {
          artifact androidSourcesJar
          artifact androidJavadocsJar
          from project.components.release

          groupId POM_GROUP
          artifactId project.name
          version POM_VERSION
        }
      }
    }
  }
}

allprojects {
  apply plugin: "com.diffplug.spotless"
  apply plugin: 'com.github.ben-manes.versions'

  spotless {
    java {
      target "src/**/*.java"

      removeUnusedImports()
      trimTrailingWhitespace()
      endWithNewline()
      indentWithSpaces(2)
    }
    kotlin {
      target "src/**/*.kt"
      ktfmt()

      trimTrailingWhitespace()
      endWithNewline()
      indentWithSpaces(2)
    }
    groovyGradle {
      target "*.gradle"
      greclipse()

      trimTrailingWhitespace()
      endWithNewline()
      indentWithSpaces(2)
    }
  }

  def isNonStable = { version ->
    def stableKeyword = ['RELEASE', 'FINAL', 'GA'].any { version.toUpperCase().contains(it) }
    def regex = /^[0-9,.v-]+(-r)?$/
    return !stableKeyword && !(version ==~ regex)
  }

  tasks.named("dependencyUpdates").configure {
    rejectVersionIf {
      isNonStable(it.candidate.version)
    }
  }

  repositories {
    mavenLocal()
    google()
    mavenCentral()

    maven {
      url "https://plugins.gradle.org/m2/"
    }

    maven {
      url 'https://jitpack.io'
      content {
        includeGroup("com.github.pyamsoft")
      }
    }
  }

  tasks.withType(JavaCompile) {
    options.compilerArgs << '-Xlint:unchecked' << '-Xlint:deprecation'
    options.deprecation = true
  }
}
